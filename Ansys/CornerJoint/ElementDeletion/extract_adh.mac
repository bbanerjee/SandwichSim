! Extract stresses in adhesive as afunction of time
!
/post1
/uis, msgpop, 3

!----------------------------------------------------------------
! Open the results file and a file for writing results
!----------------------------------------------------------------
! file,'run_bra_axial_ekill_a','rst','.'  
! *cfopen, AxEkill_Adh, dat
! file,'run_bra_mom_ekill_a','rst','.'  
! *cfopen, MomEkill_Adh, dat
file,'run_bra_trans_ekill_a','rst','.'  
*cfopen, TrEkill_Adh, dat
! file,'run_bra_axial_ekill_a_core','rst','.'  
! *cfopen, AxEkillCore_Adh, dat
! file,'run_bra_mom_ekill_a_core','rst','.'  
! *cfopen, MomEkillCore_Adh, dat
! file,'run_bra_trans_ekill_a_core','rst','.'  
! *cfopen, TrEkillCore_Adh, dat

!----------------------------------------------------------------
! Choose the first set
!----------------------------------------------------------------
allsel
set, first

!----------------------------------------------------------------
! Select nodes in the bottom adhesive layer and create a component
!----------------------------------------------------------------
xdiff = xmin_panel_ins_adh-xmin_panel_adh
esel, s, mat, , mat_adh
nsle, s, all
nsel, r, loc, x, xmin_panel_adh+0.6*xdiff, xmin_panel_ins_adh
nsel, r, loc, y, ymin_panel_face, ymin_panel_face+adh_thick_core
esln, r
cm, elem_adh, elem
*get, numElem, elem, 0, count
*get, minElem, elem, 0, num, min

!----------------------------------------------------------------
! Create an element table
!----------------------------------------------------------------
etable, stress, s, xy

!----------------------------------------------------------------
! Get the number of steps in the data file
! and Loop through the steps
!----------------------------------------------------------------
*get, numStep, active, 0, set, nset

!----------------------------------------------------------------
! Loop through the steps
!----------------------------------------------------------------
*dim, sig_adh, array, 1
*do, jj, 1, numStep-1

  !----------------------------------------------------------------
  ! Get the current time
  !----------------------------------------------------------------
  *get, curTime, active, 0, set, time

  !----------------------------------------------------------------
  ! Save the time and number of elements
  !----------------------------------------------------------------
  allsel
  cmsel, s, elem_adh, elem
  curElem = minElem
  *do, ii, 1, numElem
    !----------------------------------------------------------------
    ! Read the element data
    !----------------------------------------------------------------
    *get, sig_adh(1), elem, curElem, etab, stress

    !----------------------------------------------------------------
    ! Write the element data
    !----------------------------------------------------------------
    *vwrite, numElem, curElem, curTime, sig_adh(1)
    (f6.0,1x,f6.0,1x,e16.7,1x,e16.7)

    !----------------------------------------------------------------
    ! get the next element
    !----------------------------------------------------------------
    *get, curElem, elem, curElem, nxth
  *enddo

  !----------------------------------------------------------------
  ! Go to next set and refill etable
  !----------------------------------------------------------------
  set, next
  etable, refl
*end do

!----------------------------------------------------------------
! Close the file
!----------------------------------------------------------------
*cfclose

