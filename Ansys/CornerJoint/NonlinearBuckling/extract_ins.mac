! Extract stresses in adhesive as afunction of time
!
/post1
/uis, msgpop, 3

!----------------------------------------------------------------
! Open the results file and a file for writing results
!----------------------------------------------------------------
! file,'run_bra_axial_nlbuck_b','rst','.'  
! *cfopen, AxNLBuck_b_Ins, dat
file,'run_bra_mom_nlbuck_b','rst','.'  
*cfopen, MomNLBuck_b_Ins, dat
!file,'run_bra_trans_nlbuck_b','rst','.'  
!*cfopen, TrNLBuck_b_Ins, dat

!----------------------------------------------------------------
! Choose the first set
!----------------------------------------------------------------
allsel
set, first

!----------------------------------------------------------------
! Select nodes in the insert and create a component
!----------------------------------------------------------------
ksel, s, kp, , 57, 57
ksel, a, kp, , 63, 63
nslk, s
esln, s
cm, elem_ins, elem
*get, numElem, elem, 0, count
*get, minElem, elem, 0, num, min

!----------------------------------------------------------------
! Create an element table
!----------------------------------------------------------------
etable, stress, s, eqv

!----------------------------------------------------------------
! Get the number of steps in the data file
! and Loop through the steps
!----------------------------------------------------------------
*get, numStep, active, 0, set, nset

!----------------------------------------------------------------
! Loop through the steps
!----------------------------------------------------------------
*dim, sig_ins, array, 1
*do, jj, 1, numStep-1

  !----------------------------------------------------------------
  ! Get the current time
  !----------------------------------------------------------------
  *get, curTime, active, 0, set, time

  !----------------------------------------------------------------
  ! Save the time and number of elements
  !----------------------------------------------------------------
  allsel
  cmsel, s, elem_ins, elem
  curElem = minElem
  *do, ii, 1, numElem
    !----------------------------------------------------------------
    ! Read the element data
    !----------------------------------------------------------------
    *get, sig_ins(1), elem, curElem, etab, stress

    !----------------------------------------------------------------
    ! Write the element data
    !----------------------------------------------------------------
    *vwrite, numElem, curElem, curTime, sig_ins(1)
    (f6.0,1x,f6.0,1x,e16.7,1x,e16.7)

    !----------------------------------------------------------------
    ! get the next element
    !----------------------------------------------------------------
    *get, curElem, elem, curElem, nxth
  *enddo

  !----------------------------------------------------------------
  ! Go to next set and refill etable
  !----------------------------------------------------------------
  set, next
  etable, refl
*end do

!----------------------------------------------------------------
! Close the file
!----------------------------------------------------------------
*cfclose

